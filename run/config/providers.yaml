sessionExpiration: 48h
providers:
  - issuerURL: http://localhost:5556/dex
    callbackURI: http://localhost:8000/_authz/callback
    clientID: podinfo
    # clientSecret: test1234 # omit for PKCE auth
    cookieNamePrefix: podinfo
    # secureCookie: true # disable for local development
    logout:
      redirectURI: http://localhost:5556/dex/end-session
      path: /_authz/logout
    preAuthPolicy: |
      package authz

      import rego.v1
      import input.attributes.request.http as httpreq

      default allow = false
      default bypass_auth = false

      jwks_url := "http://localhost:5556/dex/keys"

      allow if {
        action_allowed
      }

      bypass_auth if {
        pre_authed
      }

      # allow callback/logout path
      action_allowed if {
        httpreq.method == "GET"
        glob.match("/_authz/*", ["/"], httpreq.path)
      }

      action_allowed if {
        httpreq.method == "GET"
        glob.match("/api/info", ["/"], httpreq.path)
      }

      action_allowed if {
        httpreq.method == "GET"
        glob.match("/", ["/"], httpreq.path)
      }

      pre_authed if {
        token.valid
      	now := time.now_ns() / 1000000000
        now < token.payload.exp
        token.payload.email == "kilgore@kilgore.trout"
      }

      jwks_request(url) := http.send({
        "url": url, "method": "GET", "force_cache": true, "force_cache_duration_seconds": 3600
      })

      token := { "payload": payload, "valid": valid } if {
        [_, parsed_jwt] := split(httpreq.headers["authorization"], " ")
        jwks := jwks_request(jwks_url).raw_body
        valid := io.jwt.verify_rs256(parsed_jwt, jwks)
        [_, payload, _] := io.jwt.decode(parsed_jwt)
      }

    postAuthPolicy: |
      package authz
      import rego.v1

      default allow = false

      allow if {
        token.payload.email == "kilgore@kilgore.trout"
      }

      token := { "payload": payload } if {
        [_, payload, _] := io.jwt.decode(input.parsed_jwt)
      }
    scopes:
      - openid
      - profile
      - email
      - offline_access
    headerMatch:
      name: :authority
      exact: localhost:8000
